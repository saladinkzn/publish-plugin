apply(plugin: 'maven-publish')

if(!project.tasks.findByName('classes')) {
  throw new GradleException("Non-JVM projects are not supported yet")
}

// TODO: do not add this tasks if they're already exists.
task('sourcesJar', type: org.gradle.jvm.tasks.Jar, dependsOn: project.classes, description: 'Creates sources jar') {
  classifier = 'sources'
  from sourceSets.main.allSource
}

task('javadocJar', type: org.gradle.jvm.tasks.Jar, description: 'Creates javadoc jar') {
  dependsOn 'javadoc'
}

artifacts {
  archives sourcesJar, javadocJar
}

def rootProject = project

publishing.publications {
    pluginMaven(MavenPublication) {
      pom.withXml {
        asNode().with {
          appendNode('name', rootProject.name)
          appendNode('description', rootProject.description)
          appendNode('url', rootProject.project_website)
          appendNode('scm').with {
            appendNode('url', rootProject.project_scm)
            appendNode('connection', rootProject.project_scm)
            appendNode('developerConnection', rootProject.project_scm)
          }
          appendNode('licenses').with {
            appendNode('license').with {
              appendNode('name', rootProject.license)
              appendNode('url', rootProject.license_url)
              appendNode('distribution', 'repo')
            }
          }
          appendNode('developers').with {
            appendNode('developer').with {
              appendNode('id', rootProject.developerId)
              appendNode('name', rootProject.developerName)
            }
          }
          it
        }
      }

      from components.java
//
      artifact sourcesJar {
        classifier 'sources'
      }

      artifact javadocJar {
        classifier "javadoc"
      }
    }

  marker(MavenPublication) {
    artifactId = 'ru.shadam.publish-plugin' + '.gradle.plugin'
    groupId = 'ru.shadam.publish'
    pom.withXml {
      def root = it.asElement()
      def document = root.ownerDocument
      def dependencies = root.appendChild(document.createElement("dependencies"))
      def dependency = dependencies.appendChild(document.createElement("dependency"))
      def groupId = dependency.appendChild(document.createElement("groupId"))
      groupId.textContent = project.group
      def artifactId = dependency.appendChild(document.createElement("artifactId"))
      artifactId.textContent = project.name
      def version = dependency.appendChild(document.createElement("version"))
      version.textContent = project.version
      def scope = dependency.appendChild(document.createElement('scope'))
      scope.textContent = 'compile'
    }
  }
}

project.tasks.build.finalizedBy project.tasks.publishToMavenLocal